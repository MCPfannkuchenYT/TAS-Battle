
plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

version = project.mod_version
group = project.maven_group

Set<String> apiModules = [
    "fabric-api-base",
    "fabric-command-api-v1",
    "fabric-networking-api-v1"
]

repositories {
	maven { url = 'https://maven.mgnet.work' }
	maven { url 'https://jitpack.io' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	include group: 'net.bramp.ffmpeg', name: 'ffmpeg', version: '0.6.2'
	include 'com.github.JnCrMx:discord-game-sdk4j:v0.5.4'
	implementation group: 'net.bramp.ffmpeg', name: 'ffmpeg', version: '0.6.2'
	implementation 'com.github.JnCrMx:discord-game-sdk4j:v0.5.4'
	
	apiModules.forEach {
	    include(modImplementation(fabricApi.module(it, "0.37.0+1.17")))
	}
}

minecraft {
	accessWidener = file("src/main/resources/lotas.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}